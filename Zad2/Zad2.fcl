FUNCTION_BLOCK wysokosc_napiwku // Block definition 

VAR_INPUT				
	obsluga: REAL;
	jedzenie: REAL;
END_VAR

VAR_OUTPUT				
	napiwek: REAL;
END_VAR

FUZZIFY obsluga			
	TERM kiepska := (0, 1) (4, 0) ; 
	TERM przecietna := (1, 0) (4, 0.8) (5, 1) (6, 0.8)(9, 0);
	TERM znakomita := (6, 0) (7, 0.6)(10, 1);
END_FUZZIFY

FUZZIFY jedzenie			
	TERM niezbyt_smaczne := (0, 1) (1, 1) (2, 0.8) (6,0) ;
	TERM bardzo_smaczne := (4, 0) (8, 0.5)(10, 1);
END_FUZZIFY

DEFUZZIFY napiwek			
	TERM skromny := (0,0) (5,1) (10,0);
	TERM sredni := (10,0) (15,1) (20,0);
	TERM hojny := (20,0) (25,1) (30,0);
	METHOD : COG;		
	DEFAULT := 0;		
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			
	ACT : MIN;			
	ACCU : MAX;			

	RULE 1 : IF obsluga IS kiepska OR jedzenie IS  niezbyt_smaczne THEN napiwek IS skromny;
	RULE 2 : IF obsluga IS przecietna THEN napiwek IS sredni;
	RULE 3 : IF obsluga IS znakomita AND jedzenie IS bardzo_smaczne THEN napiwek IS hojny; 
END_RULEBLOCK

END_FUNCTION_BLOCK